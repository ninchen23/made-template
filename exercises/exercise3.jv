
pipeline CountryStatsPipeline {
    // base pipeline
    WorldDevelopmentReportExtractor
        -> WorldDevelopmentReportXLSXInterpreter
        -> FigureSheetPicker
        -> FinanceCellRangeSelector
        -> RenameHeaderCellWriter;

    // further pipeline for gdpPerCapita
    RenameHeaderCellWriter
        -> gdpPerCapitaTableInterpreter
        -> gdpPerCapitaLoader;

    // further pipeline for bondIssuance
    RenameHeaderCellWriter
        -> bondIssuanceTableInterpreter
        -> bondIssuanceLoader;

    // blocks of base pipeline
    block WorldDevelopmentReportExtractor oftype HttpExtractor {
        url: "https://thedocs.worldbank.org/en/doc/7d852628d96b9411d43e5d36d5dff941-0050062022/original/Graphs-Chapter-5-02082022.xlsx";
    }
    block WorldDevelopmentReportXLSXInterpreter oftype XLSXInterpreter { }
    block FigureSheetPicker oftype SheetPicker {
        sheetName: "Figure S5.1.2";
    }
    block FinanceCellRangeSelector oftype CellRangeSelector {
        select: range P2:S45;
    }
    block RenameHeaderCellWriter oftype CellWriter {
        at: range A1:D1;
        write: [
            "Country Code",
            "Economy",
            "GDP per Capita",
            "Bond Issuance Share"
        ];
    }


    // blocks and types/constraints of further pipeline for gdpPerCapita
    block bondIssuanceTableInterpreter oftype TableInterpreter {
        header: true;
        columns: [
            "Country Code" oftype CountryCodeAlpha3,
            "Bond Issuance Share" oftype BondIssuanceShare
        ];
    }
    valuetype BondIssuanceShare oftype decimal {
        constraints: [
            BondIssuanceShareConstraint
        ];
    }
    constraint BondIssuanceShareConstraint oftype RangeConstraint {
        lowerBound: 0;
        upperBound: 1;
    }
    block bondIssuanceLoader oftype SQLiteLoader {
        table: "bondIssuance";
        file: "./country-stats.sqlite";
    }


    // blocks and types/constraints of further pipeline for gdpPerCapita
    block gdpPerCapitaTableInterpreter oftype TableInterpreter {
        header: true;
        columns: [
            "Country Code" oftype CountryCodeAlpha3,
            "GDP per Capita" oftype gdpPerCapita
        ];
    }
    valuetype gdpPerCapita oftype decimal {
        constraints: [
            gdpPerCapitaConstraint
        ];
    }
    constraint gdpPerCapitaConstraint oftype RangeConstraint {
        lowerBound: 0;
    }
    block gdpPerCapitaLoader oftype SQLiteLoader {
        table: "gdpPerCapita";
        file: "./country-stats.sqlite";
    }
}

